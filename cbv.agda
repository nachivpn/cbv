module cbv where

open import Function using (_âˆ˜_)

data Ty : Set where
  Unit   : Ty
  String : Ty
  _â‡’_    : Ty â†’ Ty â†’ Ty
  _+_    : Ty â†’ Ty â†’ Ty

variable
    a b c d : Ty

infixl 6 _`,_
infix 5 _âŠ†_

data Ctx : Set where
  [] : Ctx
  _`,_ : Ctx â†’ Ty â†’ Ctx

private
  variable
    Î“ Î“' Î” Î”' Î˜ Î˜' : Ctx

---------------------------------
-- Variables, terms and weakening
---------------------------------

data Var : Ctx â†’ Ty â†’ Set where
  zero : Var (Î“ `, a) a
  succ : (v : Var Î“ a) â†’ Var (Î“ `, b) a

data Tm : Ctx â†’ Ty â†’ Set where

  var  : Var Î“ a
         -------
       â†’ Tm Î“ a

  lam  : Tm (Î“ `, a) b
         -------------
       â†’ Tm Î“ (a â‡’ b)

  app  : Tm Î“ (a â‡’ b) â†’ Tm Î“ a
         ---------------------
       â†’ Tm Î“ b

  let-in : Tm Î“ a â†’ Tm (Î“ `, a) b
           ----------------------
         â†’ Tm Î“ b

  unit  : Tm Î“ Unit

  print : Tm Î“ (String â‡’ Unit)

  inl   : Tm Î“ a
          ------------
        â†’ Tm Î“ (a + b)

  inr   : Tm Î“ b
          ------------
        â†’ Tm Î“ (a + b)

  case  : Tm Î“ (a + b) â†’ Tm (Î“ `, a) c â†’ Tm (Î“ `, b) c
          --------------------------------------------
        â†’ Tm Î“ c

-- Order-preserving embeddings (OPEs)
data _âŠ†_  : Ctx â†’ Ctx â†’ Set where
  base : [] âŠ† []
  drop : (w : Î“ âŠ† Î”) â†’ Î“ âŠ† Î” `, a
  keep : (w : Î“ âŠ† Î”) â†’ Î“ `, a âŠ† Î” `, a

-- identity OPE
idWk[_] : (Î“ : Ctx) â†’ Î“ âŠ† Î“
idWk[_] []        = base
idWk[_] (Î“ `, _a) = keep  idWk[ Î“ ]

idWk = Î» {Î“} â†’ idWk[ Î“ ]

-- composition of OPEs
_âˆ™_ : Î˜ âŠ† Î” â†’ Î” âŠ† Î“ â†’ Î˜ âŠ† Î“
w       âˆ™ base     = w
w       âˆ™ drop  w' = drop  (w âˆ™ w')
drop  w âˆ™ keep  w' = drop  (w âˆ™ w')
keep  w âˆ™ keep  w' = keep  (w âˆ™ w')

-- OPE used to weaken the context with a fresh variable
freshWk : Î“ âŠ† Î“ `, a
freshWk = drop idWk

freshWk[_] = Î» {Î“} a â†’ freshWk {Î“} {a}

wkVar : Î“ âŠ† Î“' â†’ Var Î“ a â†’ Var Î“' a
wkVar (drop w) v        = succ (wkVar w v)
wkVar (keep w) zero     = zero
wkVar (keep w) (succ v) = succ (wkVar w v)

wkTm : Î“ âŠ† Î“' â†’ Tm Î“ a â†’ Tm Î“' a
wkTm w (var x)        = var (wkVar w x)
wkTm w (lam t)        = lam (wkTm (keep w) t)
wkTm w (app t u)      = app (wkTm w t) (wkTm w u)
wkTm w (let-in t u)   = let-in (wkTm w t) (wkTm (keep w) u)
wkTm w unit           = unit
wkTm w print          = print
wkTm w (inl t)        = inl (wkTm w t)
wkTm w (inr t)        = inr (wkTm w t)
wkTm w (case t uâ‚ uâ‚‚) = case (wkTm w t) (wkTm (keep w) uâ‚) (wkTm (keep w) uâ‚‚)

------------------
-- Model artifacts
------------------

open import Data.Unit  using (âŠ¤ ; tt)
open import Data.Product  using (Î£ ; _Ã—_ ; _,_)
open import Data.Sum using (_âŠ_ ; injâ‚ ; injâ‚‚ ; [_,_])

variable
  A B C : Ctx â†’ Set

-- family of maps between interpretations
_â†’Ì‡_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ Set
_â†’Ì‡_ A B = {Î” : Ctx} â†’ A Î” â†’ B Î”

infixr 10 _â†’Ì‡_

-- exponential family
_â‡’'_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ (Ctx â†’ Set)
_â‡’'_ A B Î“ = {Î“' : Ctx} â†’ Î“ âŠ† Î“' â†’ A Î“' â†’ B Î“'

-- product family
_Ã—'_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ (Ctx â†’ Set)
_Ã—'_ A B Î“ = A Î“ Ã— B Î“

-- sum family
_âŠ'_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ (Ctx â†’ Set)
_âŠ'_ A B Î“ = A Î“ âŠ B Î“

-- unit family
âŠ¤' : (Ctx â†’ Set)
âŠ¤' = Î» Î“ â†’ âŠ¤

-------------
-- Evaluation
-------------

module Model
  -- these parameters interpret base types (Filinski's â„¬ function)
  (String' : Ctx â†’ Set)
  (wkString' : {Î“ Î“' : Ctx} â†’ Î“ âŠ† Î“' â†’ String' Î“ â†’ String' Î“')
  -- these parameters are that of the monad (Filinski's TÊ³ monad)
  (ğ’¯ : (Ctx â†’ Set) â†’ (Ctx â†’ Set))
  (Î·  : {A : Ctx â†’ Set} â†’ A â†’Ì‡ ğ’¯ A)
  (bind-int : {A B : Ctx â†’ Set} â†’ (A â‡’' ğ’¯ B) â†’Ì‡ (ğ’¯ A â‡’' ğ’¯ B))
  where

  -- Filinski's extension operator
  _â‹†_ : ğ’¯ A Î“ â†’ (A â‡’' ğ’¯ B) Î“ â†’ ğ’¯ B Î“
  w â‹† f = bind-int f idWk w

  -- interpretation of types
  Tm'- : Ty â†’ (Ctx â†’ Set)
  Tm'- Unit    = âŠ¤'
  Tm'- String  = String'
  Tm'- (a â‡’ b) = (Tm'- a) â‡’' ğ’¯ (Tm'- b)
  Tm'- (a + b) = Tm'- a âŠ' Tm'- b

  -- interpretation of contexts
  Sub'- : Ctx â†’ (Ctx â†’ Set)
  Sub'- []       = âŠ¤'
  Sub'- (Î“ `, a) = Sub'- Î“ Ã—' Tm'- a

  -- monotonicity lemma
  wkTm'- : Î“ âŠ† Î“' â†’ Tm'- a Î“ â†’ Tm'- a Î“'
  wkTm'- {a = Unit}   w x = x
  wkTm'- {a = String} w x = wkString' w x
  wkTm'- {a = a â‡’ b}  w f = Î» w' y â†’ f (w âˆ™ w') y
  wkTm'- {a = a + b}  w x = [ injâ‚ âˆ˜ wkTm'- {a = a} w , injâ‚‚ âˆ˜ wkTm'- {a = b} w ] x

  -- monotonicity lemma
  wkSub'- : Î“ âŠ† Î“' â†’ Sub'- Î” Î“ â†’ Sub'- Î” Î“'
  wkSub'- {Î” = []}     w tt      = tt
  wkSub'- {Î” = Î” `, a} w (s , x) = wkSub'- {Î” = Î”} w s , wkTm'- {a = a} w x

  -- interpretation of variables
  lookup' : Var Î“ a â†’ (Sub'- Î“ â†’Ì‡ Tm'- a)
  lookup' zero     (_ , x) = x
  lookup' (succ x) (Î³ , _) = lookup' x Î³

  -- these parameters interpret constants, and correspond to Filinski's ğ’ function
  module Eval (print' : {Î“ : Ctx} â†’ ğ’¯ (Tm'- (String â‡’ Unit)) Î“) where

    -- interpretation of terms
    eval : Tm Î“ a â†’ (Sub'- Î“ â†’Ì‡ ğ’¯ (Tm'- a))
    eval {Î“} (var x)        s = Î· (lookup' x s)
    eval {Î“} (lam t)        s = Î· (Î» {_} w x â†’ eval t ((wkSub'- {Î” = Î“} w s) , x))
    eval {Î“} (app t u)      s = eval t s â‹† (Î» w f â†’ eval u (wkSub'- {Î” = Î“} w s) â‹† f)
    eval {Î“} (let-in t u)   s = eval t s â‹† (Î» w x â†’ eval u (wkSub'- {Î” = Î“} w s , x))
    eval {Î“} unit           s = Î· tt
    eval {Î“} print          s = print'
    eval {Î“} (inl t)        s = eval t s â‹† Î» w x â†’ Î· (injâ‚ x)
    eval {Î“} (inr t)        s = eval t s â‹† Î» w x â†’ Î· (injâ‚‚ x)
    eval {Î“} (case t uâ‚ uâ‚‚) s = eval t s
      â‹† Î» { w (injâ‚ x) â†’ eval uâ‚ (wkSub'- {Î” = Î“} w s , x)
          ; w (injâ‚‚ y) â†’ eval uâ‚‚ (wkSub'- {Î” = Î“} w s , y) }

---------------
-- Normal forms
---------------

data At : Ctx â†’ Ty â†’ Set
data Nv : Ctx â†’ Ty â†’ Set
data Nc : Ctx â†’ Ty â†’ Set

-- Atoms
data At where
  var   : Var Î“ a â†’ At Î“ a
  print : At Î“ (String â‡’ Unit)

-- Normal values
data Nv where
  str   : At Î“ String â†’ Nv Î“ String
  unit  : Nv Î“ Unit
  lam   : Nc (Î“ `, a) b â†’ Nv Î“ (a â‡’ b)
  inl   : Nv Î“ a â†’ Nv Î“ (a + b)
  inr   : Nv Î“ b â†’ Nv Î“ (a + b)

-- Normal computations
data Nc where
  ret          : Nv Î“ a â†’ Nc Î“ a
  let-app-in   : At Î“ (a â‡’ b) â†’ Nv Î“ a â†’ Nc (Î“ `, b) c â†’ Nc Î“ c
  case         : At Î“ (a + b) â†’ Nc (Î“ `, a) c â†’ Nc (Î“ `, b) c â†’ Nc Î“ c

embAt : At Î“ a â†’ Tm Î“ a
embAt (var x) = var x
embAt print   = print

embNv : Nv Î“ a â†’ Tm Î“ a
embNc : Nc Î“ a â†’ Tm Î“ a

embNv (str x) = embAt x
embNv unit    = unit
embNv (lam m) = lam (embNc m)
embNv (inl n) = inl (embNv n)
embNv (inr n) = inr (embNv n)

embNc (ret x)            = embNv x
embNc (let-app-in x n c) = let-in (app (embAt x) (embNv n)) (embNc c)
embNc (case x mâ‚ mâ‚‚)     = case (embAt x) (embNc mâ‚) (embNc mâ‚‚)

wkAt : Î“ âŠ† Î“' â†’ At Î“ a â†’ At Î“' a
wkAt w (var x) = var (wkVar w x)
wkAt w print   = print

wkNv : Î“ âŠ† Î“' â†’ Nv Î“ a â†’ Nv Î“' a
wkNc : Î“ âŠ† Î“' â†’ Nc Î“ a â†’ Nc Î“' a

wkNv w (str x) = str (wkAt w x)
wkNv w unit    = unit
wkNv w (lam m) = lam (wkNc (keep w) m)
wkNv w (inl n) = inl (wkNv w n)
wkNv w (inr n) = inr (wkNv w n)

wkNc w (ret x)            = ret (wkNv w x)
wkNc w (let-app-in x n m) = let-app-in (wkAt w x) (wkNv w n) (wkNc (keep w) m)
wkNc w (case x mâ‚ mâ‚‚)     = case (wkAt w x) (wkNc (keep w) mâ‚) (wkNc (keep w) mâ‚‚)

-- some pretentious notation
Var- Tm- At- Nv- Nc- : Ty â†’ Ctx â†’ Set
Var- a Î“ = Var Î“ a
Tm- a Î“ = Tm Î“ a
At- a Î“ = At Î“ a
Nv- a Î“ = Nv Î“ a
Nc- a Î“ = Nc Î“ a

------------
-- NbE model
------------

module ResidualisingMonad where

  -- data structure used to define a monad on families ('ğ’' for "cover", following Abel)
  -- (similar to Lindley's "free" monad)
  data ğ’ (A : Ctx â†’ Set) : Ctx â†’ Set where
    ret        : A â†’Ì‡ ğ’ A
    let-app-in : At Î“ (a â‡’ b) â†’ Nv Î“ a â†’ ğ’ A (Î“ `, b) â†’ ğ’ A Î“
    case       : At Î“ (a + b) â†’ ğ’ A (Î“ `, a) â†’ ğ’ A (Î“ `, b) â†’ ğ’ A Î“

  module _ (wkA : {Î” Î”' : Ctx} â†’ Î” âŠ† Î”' â†’ A Î” â†’ A Î”') where
    wkğ’ : Î“ âŠ† Î“' â†’ ğ’ A Î“ â†’ ğ’ A Î“'
    wkğ’ w (ret x)            = ret (wkA w x)
    wkğ’ w (let-app-in x n c) = let-app-in (wkAt w x) (wkNv w n) (wkğ’ (keep w) c)
    wkğ’ w (case x mâ‚ mâ‚‚)     = case (wkAt w x) (wkğ’ (keep w) mâ‚) (wkğ’ (keep w) mâ‚‚)

  join : ğ’ (ğ’ A) â†’Ì‡ ğ’ A
  join (ret x)            = x
  join (let-app-in x n m) = let-app-in x n (join m)
  join (case x mâ‚ mâ‚‚)     = case x (join mâ‚) (join mâ‚‚)

  fmap : (A â†’Ì‡ B) â†’ (ğ’ A â†’Ì‡ ğ’ B)
  fmap f (ret x)            = ret (f x)
  fmap f (let-app-in x n m) = let-app-in x n (fmap f m)
  fmap f (case x mâ‚ mâ‚‚)     = case x (fmap f mâ‚) (fmap f mâ‚‚)

  bind : (A â†’Ì‡ ğ’ B) â†’ (ğ’ A â†’Ì‡ ğ’ B)
  bind f x = join (fmap f x)

  fmap-int : (A â‡’' B) â†’Ì‡ (ğ’ A â‡’' ğ’ B)
  fmap-int f w (ret x)            = ret (f w x)
  fmap-int f w (let-app-in x n m) = let-app-in x n (fmap-int f (drop w) m)
  fmap-int f w (case x mâ‚ mâ‚‚)     = case x (fmap-int f (drop w) mâ‚) (fmap-int f (drop w) mâ‚‚)

  bind-int : (A â‡’' ğ’ B) â†’Ì‡ (ğ’ A â‡’' ğ’ B)
  bind-int f w (ret x)            = f w x
  bind-int f w (let-app-in x n m) = let-app-in x n (bind-int f (drop w) m)
  bind-int f w (case x mâ‚ mâ‚‚)     = case x (bind-int f (drop w) mâ‚) (bind-int f (drop w) mâ‚‚)

  -- (special case of) Filinski's Î³áµÊ³
  disperse : Nc- a â†’Ì‡ ğ’ (Nv- a)
  disperse (ret nv)           = ret nv
  disperse (let-app-in x n m) = let-app-in x n (disperse m)
  disperse (case x mâ‚ mâ‚‚)     = case x (disperse mâ‚) (disperse mâ‚‚)

  -- (special case of) Filinski's collect
  collect : ğ’ (Nv- a) â†’Ì‡ Nc- a
  collect (ret nv)           = ret nv
  collect (let-app-in x n m) = let-app-in x n (collect m)
  collect (case x mâ‚ mâ‚‚)     = case x (collect mâ‚) (collect mâ‚‚)

  -- Filinski's bind
  register-let-app : At Î“ (a â‡’ b) â†’ Nv Î“ a â†’ ğ’ (Var- b) Î“
  register-let-app x nv = let-app-in x nv (ret zero)

  -- Filinski's binds
  register-case : At Î“ (a + b) â†’ ğ’ (Var- a âŠ' Var- b) Î“
  register-case x = case x (ret (injâ‚ zero)) (ret (injâ‚‚ zero))

  --
  run : ğ’ (Nc- a) â†’Ì‡ Nc- a
  run m = collect (join (fmap disperse m))

-- A "residualising" monad has the following exported operations.
-- Observe that it can be defined in ways other than ğ’.
-- For e.g., it could have been defined using continuations (as in Filinski)
open ResidualisingMonad
  using (ğ’ ; wkğ’ ; bind-int
        ; register-let-app ; register-case
        ; disperse ; collect ; run)
  renaming (ret to Î·)

open Model (At- String) wkAt ğ’ Î· bind-int

reflect : At- a â†’Ì‡ ğ’ (Tm'- a)
reify   : Tm'- a â†’Ì‡ Nc- a

reflect {Unit}   x = Î· tt
reflect {String} x = Î· x
reflect {a â‡’ b}  x = Î·
  (Î» w xa â†’ disperse {a} (reify xa)
    â‹† Î» w'  nva â†’ register-let-app (wkAt (w âˆ™ w') x) nva
    â‹† Î» w'' vb â†’ reflect (var vb))
reflect {a + b} x  = register-case x
  â‹† Î» { w (injâ‚ v) â†’ reflect (var v) â‹† Î» w' z â†’ Î· (injâ‚ z)
      ; w (injâ‚‚ v) â†’ reflect (var v) â‹† Î» w' z â†’ Î· (injâ‚‚ z) }

reify {Unit}   tt      = ret unit
reify {String} x       = ret (str x)
reify {a â‡’ b}  f       = ret (lam (collect
  (reflect (var zero)
    â‹† Î» w  xa â†’ f (freshWk âˆ™ w) xa
    â‹† Î» w' xb â†’ disperse (reify xb))))
reify {a + b} (injâ‚ x) = collect
  (disperse (reify {a} x)
  â‹† (Î» w nv â†’ Î· (inl nv)))
reify {a + b} (injâ‚‚ y) = collect
  (disperse (reify {b} y)
  â‹† (Î» w nv â†’ Î· (inr nv)))

idâ‚› : ğ’ (Sub'- Î“) Î“
idâ‚› {[]}     = Î· tt
idâ‚› {Î“ `, a} = reflect (var zero)
  â‹† Î» w x â†’ wkğ’ (wkSub'- {Î” = Î“}) (freshWk âˆ™ w) idâ‚›
  â‹† Î» w' s â†’ Î· (s , (wkTm'- {a = a} w' x))

open Eval (reflect print)

norm : Tm- a â†’Ì‡ Nc- a
norm t = run
  (idâ‚›
  â‹† Î» w s â†’ eval t s
  â‹† Î» w' x â†’ Î· (reify x))

-------------------------
-- References and related
-------------------------

-- Filinski: Normalization by Evaluation for the Computational Lambda-Calculus (https://link.springer.com/chapter/10.1007/3-540-45413-6_15)
-- Abel & Sattler: Normalization by Evaluation for Call-By-Push-Value [...] (https://www.cse.chalmers.se/~abela/ppdp19.pdf)
-- Lindley: Normalisation by evaluation (https://homepages.inf.ed.ac.uk/slindley/nbe/nbe-cambridge2016.pdf)
-- Valliappan, Russo & Lindley: Practical NbE for EDSLs (https://nachivpn.me/nbe-edsl.pdf)
