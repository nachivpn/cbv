module Model where

open import CLC

open import Function using (_âˆ˜_)
open import Data.Unit  using (âŠ¤ ; tt)
open import Data.Product  using (Î£ ; _Ã—_ ; _,_)
open import Data.Sum using (_âŠ_ ; injâ‚ ; injâ‚‚ ; [_,_])

------------------
-- Model artifacts
------------------

variable
  A B C : Ctx â†’ Set

-- family of maps between interpretations
_â†’Ì‡_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ Set
_â†’Ì‡_ A B = {Î” : Ctx} â†’ A Î” â†’ B Î”

infixr 10 _â†’Ì‡_

-- exponential family
_â‡’'_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ (Ctx â†’ Set)
_â‡’'_ A B Î“ = {Î“' : Ctx} â†’ Î“ âŠ† Î“' â†’ A Î“' â†’ B Î“'

-- product family
_Ã—'_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ (Ctx â†’ Set)
_Ã—'_ A B Î“ = A Î“ Ã— B Î“

-- sum family
_+'_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ (Ctx â†’ Set)
_+'_ A B Î“ = A Î“ âŠ B Î“

-- unit family
âŠ¤' : (Ctx â†’ Set)
âŠ¤' = Î» Î“ â†’ âŠ¤

-------------
-- Evaluation
-------------

module Core
  -- these parameters interpret base types (Filinski's â„¬ function)
  (String' : Ctx â†’ Set)
  (wkString' : {Î“ Î“' : Ctx} â†’ Î“ âŠ† Î“' â†’ String' Î“ â†’ String' Î“')
  -- these parameters are that of a monad (Filinski's T monad)
  (ğ’¯ : (Ctx â†’ Set) â†’ (Ctx â†’ Set))
  (wkğ’¯ : {A : Ctx â†’ Set} â†’ ({Î” Î”' : Ctx} â†’ Î” âŠ† Î”' â†’ A Î” â†’ A Î”') â†’ {Î“ Î“' : Ctx} â†’ Î“ âŠ† Î“' â†’ ğ’¯ A Î“ â†’ ğ’¯ A Î“')
  (Î·  : {A : Ctx â†’ Set} â†’ A â†’Ì‡ ğ’¯ A)
  (bind-int : {A B : Ctx â†’ Set} â†’ (A â‡’' ğ’¯ B) â†’Ì‡ (ğ’¯ A â‡’' ğ’¯ B))
  where

  -- Filinski's extension operator
  _â‹†_ : ğ’¯ A Î“ â†’ (A â‡’' ğ’¯ B) Î“ â†’ ğ’¯ B Î“
  w â‹† f = bind-int f idWk w

  -- interpretation of types
  Tm'- : Ty â†’ (Ctx â†’ Set)
  Tm'- Unit    = âŠ¤'
  Tm'- String  = String'
  Tm'- (a â‡’ b) = (Tm'- a) â‡’' ğ’¯ (Tm'- b)
  Tm'- (a + b) = Tm'- a +' Tm'- b

  -- interpretation of contexts
  Env'- : Ctx â†’ (Ctx â†’ Set)
  Env'- []       = âŠ¤'
  Env'- (Î“ `, a) = Env'- Î“ Ã—' ğ’¯ (Tm'- a)

  -- monotonicity lemma
  wkTm'- : Î“ âŠ† Î“' â†’ Tm'- a Î“ â†’ Tm'- a Î“'
  wkTm'- {a = Unit}   w x = x
  wkTm'- {a = String} w x = wkString' w x
  wkTm'- {a = a â‡’ b}  w f = Î» w' y â†’ f (w âˆ™ w') y
  wkTm'- {a = a + b}  w x = [ injâ‚ âˆ˜ wkTm'- {a = a} w , injâ‚‚ âˆ˜ wkTm'- {a = b} w ] x

  -- monotonicity lemma
  wkEnv'- : Î“ âŠ† Î“' â†’ Env'- Î” Î“ â†’ Env'- Î” Î“'
  wkEnv'- {Î” = []}     w tt      = tt
  wkEnv'- {Î” = Î” `, a} w (s , x) = wkEnv'- {Î” = Î”} w s , wkğ’¯ (wkTm'- {a = a}) w x

  -- interpretation of variables
  lookup' : Var Î“ a â†’ (Env'- Î“ â†’Ì‡ ğ’¯ (Tm'- a))
  lookup' zero     (_ , x) = x
  lookup' (succ x) (Î³ , _) = lookup' x Î³

  -- these parameters interpret constants, and correspond to Filinski's ğ’ function
  module Eval (print' : {Î“ : Ctx} â†’ ğ’¯ (Tm'- (String â‡’ Unit)) Î“) where

    -- interpretation of terms
    eval : Tm Î“ a â†’ (Env'- Î“ â†’Ì‡ ğ’¯ (Tm'- a))
    eval {Î“} (var x)        s = lookup' x s
    eval {Î“} (lam t)        s = Î· (Î» {_} w x â†’ eval t ((wkEnv'- {Î” = Î“} w s) , Î· x))
    eval {Î“} (app t u)      s = eval t s â‹† (Î» w f â†’ eval u (wkEnv'- {Î” = Î“} w s) â‹† f)
    eval {Î“} (let-in t u)   s = eval t s â‹† (Î» w x â†’ eval u (wkEnv'- {Î” = Î“} w s , Î· x))
    eval {Î“} unit           s = Î· tt
    eval {Î“} print          s = print'
    eval {Î“} (inl t)        s = eval t s â‹† Î» w x â†’ Î· (injâ‚ x)
    eval {Î“} (inr t)        s = eval t s â‹† Î» w x â†’ Î· (injâ‚‚ x)
    eval {Î“} (case t uâ‚ uâ‚‚) s = eval t s
      â‹† Î» { w (injâ‚ x) â†’ eval uâ‚ (wkEnv'- {Î” = Î“} w s , Î· x)
          ; w (injâ‚‚ y) â†’ eval uâ‚‚ (wkEnv'- {Î” = Î“} w s , Î· y) }
